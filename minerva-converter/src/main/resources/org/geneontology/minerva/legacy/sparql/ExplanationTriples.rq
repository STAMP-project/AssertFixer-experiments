PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?s ?p ?o
WHERE {
    ?s ?p ?o .
    ?s a owl:NamedIndividual .
    FILTER NOT EXISTS {
        ?s ?other_rel ?o .
        ?other_rel rdfs:subPropertyOf ?p .
        FILTER(?other_rel != ?p) 
    }
    FILTER NOT EXISTS {
        ?s <http://arachne.geneontology.org/indirect_type> ?o .
    }
    FILTER(isIRI(?s))
    FILTER(isIRI(?o))
    FILTER(?o NOT IN (rdfs:Resource, owl:Thing, owl:NamedIndividual, owl:Class))
    FILTER(?p != <http://arachne.geneontology.org/indirect_type>)
}
